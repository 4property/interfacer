plugins {
    id 'idea'
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'maven-publish'
}

group = 'io.github.tunguski'
version = '0.2.0-SNAPSHOT'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'pl.matsuo.interfacer:interfacer-core:0.1.0'

    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    plugins {
        interfacer {
            id = 'io.github.tunguski.interfacer'
            implementationClass = 'pl.matsuo.interfacer.gradle.InterfacerPlugin'
        }
    }
}


pluginBundle {
    // These settings are set for the whole plugin bundle
    website = 'https://github.com/tunguski/interfacer'
    vcsUrl = 'https://github.com/tunguski/interfacer'

    plugins {
        interfacer {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle Interfacer plugin'
            description = 'Add interfaces to classes generated by other plugins'
            tags = ['interfacer', 'interfaces', 'per', 'plugin']
            version = '0.2.0-SNAPSHOT'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

publishing {
    repositories {
        maven {
            name = 'ossrh'
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Interfacer Gradle Plugin'
                description = 'Add interfaces to classes generated by other plugins'
                url = 'https://github.com/tunguski/interfacer'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'marek'
                        name = 'Marek Romanowski'
                        email = 'marek.romanowski@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:tunguski/interfacer.git'
                    developerConnection = 'scm:git:git@github.com:tunguski/interfacer.git'
                    url = 'git@github.com:tunguski/interfacer.git'
                }
            }
        }
    }
}
